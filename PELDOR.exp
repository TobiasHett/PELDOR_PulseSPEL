; Double Electron-Electron Resonance
; PulseSPEL pulse programs, phase programs & dimensions

; Dimensions
; -------------------------------------------
begin defs          ; dimension of data array [sx,sy]
 dim s[256]         ; for 2P ESE Setup
 dim1 s[256]        ; for 2P Field Sweep
 dim2 s[256]        ; for 3P ELDOR Setup
 dim3 s[100]        ; for 3P ELDOR Nutation
 dim4 s[256]        ; for 4P DEER Setup
 dim5 s[379]        ; for 4P DEER sx=(d1+d2-2*d3)/d30
end defs

; Phase cycling programs
; -------------------------------------------
begin lists "+<x> none"
 ph1 +<x>
 ph2 +<x>
 asg1 +a
 bsg1 +b
end lists

begin lists1 "-<x> none"
 ph1 -<x>
 ph2 -<x>
 asg1 +a
 bsg1 +b
end lists1

begin lists2 "2-step"
 ph1 +<x> -<x>
 ph2 +<x>
 asg1 +a -a
 bsg1 +b -b
end lists2

; PulseSPEL Programs

; Standing 2-pulse Hahn echo
; -------------------------------------------
begin exp "2P ESE Setup" [TRANS QUAD]

 shot i=1 to h           ; accumulation loop
  d9                     ; DAF
  p0 [ph1]               ; 1st pulse
  d1                     ; tau1
  p1 [ph2]               ; 2nd pulse
  d1                     ; tau1
  d0                     ; acquistion delay
  dig [sg1]              ; acquisition
 next i                  ; end of accumulation loop

end exp

; 2-pulse Field Sweep
; -------------------------------------------
begin exp1 "2P Field Sweep" [INTG QUAD]

 for k=1 to n            ; averaging loop
  totscans(n)            ; output of total number of scans
  
  bsweep x=1 to sx       ; field sweep loop
   shot i=1 to h         ; accumulation loop
    d9                   ; DAF
    p0 [ph1]             ; 1st pulse
    d1                   ; tau1
    p1 [ph2]             ; 2nd pulse
    d1                   ; tau1
    d0                   ; acquistion delay
    acq [sg1]            ; acquisition
   next i                ; end of accumulation loop
  next x                 ; end of field sweep loop

  scansdone(k)           ; output of scans done
 next k                  ; end of averaging loop

end exp1


; Standing 3-pulse Echo
; -------------------------------------------
begin exp2 "3P ELDOR Setup" [TRANS QUAD]

 shot i=1 to h           ; accumulation loop
  d9                     ; DAF
  p2 [ELDOR]             ; 1st pulse
  d4                     ; T              
  p0 [ph1]               ; 2nd pulse
  d1                     ; tau1
  p1 [ph2]               ; 3d pulse
  d1                     ; tau1
  d0                     ; acquistion delay
  dig [sg1]              ; acquisition
 next i                  ; end of accumulation loop

end exp2


; Pump Pulse Optimization
; -------------------------------------------
begin exp3 "3P ELDOR Nutation" [SPT QUAD]
 
 for k=1 to n            ; averaging loop
  totscans(n)            ; output of total number of scans
  
  dx=p3                  ; define time axis
  p4=p3                  ; initial pump pulse length
  d5=d4                  ; initial T
  
  sweep x=1 to sx        ; sweep loop
   
   shot i=1 to h         ; accumulation loop
    d9                   ; DAF
    p4 [ELDOR]           ; 1st pulse
    d5                   ; T  
    p0 [ph1]             ; 2nd pulse
    d1                   ; tau1
    p1 [ph2]             ; 3d pulse
    d1                   ; tau1
    d0                   ; acquistion delay
    acq [sg1]            ; acquisition
   next i                ; end of accumulation loop
   
   dx=dx+d6              ; define time axis increment
   p4=p4+d6              ; increment pump pulse length
   d5=d5+d6              ; increment T
  
  next x                 ; end of sweep loop
  
  scansdone(k)           ; output of scans done
 next k                  ; end of averaging loop

end exp3


; Standing 4-pulse DEER
; -------------------------------------------
begin exp4 "4P DEER Setup" [TRANS QUAD]

 shot i=1 to h           ; accumulation loop
  d9                     ; DAF
  p0 [ph1]               ; 1st pulse
  d1                     ; tau1
  p1 [ph2]               ; 2nd pulse
  d1                     ; tau1
  p2 [ELDOR]             ; 1st pulse
  d2                     ; tau2
  p1 [ph2]               ; 3d pulse
  d2                     ; tau2
  d0                     ; acquistion delay
  dig [sg1]              ; acquisition
 next i                  ; end of accumulation loop

end exp4


; 4-pulse DEER
; -------------------------------------------
begin exp5 "4P DEER" [INTG QUAD]

 for k=1 to n            ; averaging loop
  totscans(n)            ; output of total number of scans
  
  d10=d1                 ; initial tau1
  d13=d3                 ; initial deadtime delay
  
  for j=1 to m           ; tau1 averaging loop
   
   dx=0                  ; define time axis
   d11=d13               ; tau1 + initial t
   d12=d1+d2             ;
   d12=d12-d3            ; tau2 - initial t   
  
   sweep x=1 to sx       ; sweep loop
    
    shot i=1 to h        ; accumulation loop
     d9                  ; DAF
     p0 [ph1]            ; 1st pulse
     d10                 ; tau1
     p1 [ph2]            ; 2nd pulse
     d11                 ; tau1
     p2 [ELDOR]          ; 1st pulse
     d12                 ; tau2
     p1 [ph2]            ; 3d pulse
     d2                  ; tau2
     d0                  ; acquistion delay
     acq [sg1]           ; acquisition
    next i               ; end of accumulation loop
    
    dx=dx+d30            ; define time axis increment
    d11=d11+d30          ; tau1 + t
    d12=d12-d30          ; tau2 - t
    
   next x                ; end of sweep loop
   
   d10=d10+d31           ; increment tau1
   d13=d13+d31           ; increment initial deadtime delay
   
  next j                 ; end of tau1 averaging loop
  
  scansdone(k)           ; output of scans done
 next k                  ; end of averaging loop

end exp5